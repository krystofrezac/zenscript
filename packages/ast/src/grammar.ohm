BoringLang {
  space := " " | "\t" | newLine | oneLineComment | multiLineComment

  File = ListOf<Statement, #(newLine)>

  Statement = | VariableDeclaration
              | ExportedVariable
              | Expression

  newLine = " "*"\n"" "*
  expressionAssignmentOperator = "="
  typeAssignmentOperator = ":"
  oneLineComment = "//" (~newLine any)* 
  multiLineComment = "/*" (~"*/" any)* "*/"

  VariableDeclaration = | identifierExpression ExpressionAssignment                            -- onlyExpression
                        | identifierExpression TypeAssignment ExpressionAssignment             -- expressionAndType
                        | identifierExpression TypeAssignment                                  -- onlyType
  ExpressionAssignment = expressionAssignmentOperator Expression
  TypeAssignment = typeAssignmentOperator TypeExpression 

  ExportedVariable = "export" VariableDeclaration

  Expression = | ImportStatementExpression
               | FunctionCallExpression
               | FunctionDeclarationExpression
               | numberExpression
               | stringExpression
               | booleanExpression 
               | RecordExpression
               | RecordEntryAccessExpression
               | identifierExpression
               | Block
               | TupleExpression

  TypeExpression = | ImportStatementType
                   | FunctionDeclarationType
                   | stringType
                   | numberType
                   | booleanType
                   | TupleType
                   | FunctionCallType
                   | RecordType
                   | RecordEntryAccessType
                   | identifierType
                   | Block

  identifier = letter+
  identifierExpression = identifier
  identifierType = identifier

  ImportStatementExpression = "import" TupleExpression
  ImportStatementType = "import" TupleType

  numberExpression = digit+
  numberType = "number"

  stringCharacter =  ~("\"" | "\\" | newLine) any
  stringExpression = "\"" stringCharacter* "\""
  stringType = "string"

  Block = "{" ListOf<Statement, #(newLine)> "}"

  trueExpression = "true"
  falseExpression = "false"
  booleanExpression = trueExpression | falseExpression
  booleanType = "boolean"

  TupleExpression = "(" ListOf<Expression, ","> ")"
  TupleType = "(" ListOf<TypeExpression, ","> ")"


  RecordEntryExpression = identifierExpression ":" Expression
  RecordExpression = "%{" ListOf<RecordEntryExpression, ","> "}"

  RecordEntryType = identifierType ":" TypeExpression
  RecordType = "%{" ListOf<RecordEntryType, ","> "}"

  Min2List<Expression, Separator> = Expression Separator NonemptyListOf<Expression, Separator>

  RecordEntryAccessExpression = Min2List<identifierExpression, ".">
  RecordEntryAccessType = Min2List<identifierType, ".">

  FunctionParameter = identifierExpression
  FunctionDeclarationExpression = "(" ListOf<FunctionParameter, ","> ")" Expression
  FunctionDeclarationType = TupleType TypeExpression

  FunctionCallExpression = Expression TupleExpression
  FunctionCallType = TypeExpression TupleType
}